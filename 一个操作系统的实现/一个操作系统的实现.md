# 一个操作系统的实现

==所有代码在源代码文件夹==
## 马上动手写一个最小的“操作系统”

### 准备工作

- 硬件

  一台计算机(Linux或Windows)

  一张空白软盘

- 软件

  汇编编译器NASM

  软盘绝对扇区读写工具

### 十分钟完成的操作系统

```asm
		org		07c00h ; 告诉编译器程序加载到7c00处
		mov 	ax, cs
		mov 	ds, ax
		mov 	es, ax
		call DispStr ; 调用显示字符串例程
		jmp 	$ ; 无限循环
	DispStr:
		mov 	ax, BootMessage
		mov 	bp, ax ; ES:BP = 串地址
		mov 	cx, 16 ; CX = 串长度
		mov 	ax, 01301h ; AH = 13, AL = 01h
		mov 	bx, 000ch ; 页号为0 (BH = 0) 黑底红字(BL = 0Ch,高亮)
		mov 	dl, 0
		int 	10h ; 10h 号中断
		ret
	BootMessage: 			db "Hello,␣OS␣world!"
	times 510 - ($-$$)		db 0 ; 填充剩下的空间，使生成的二进制代码恰好为512字节
	dw 0xaa55 ; 结束标志
```

上分代码用NASM编译一下：

```
nasm boot.asm -o boot.bin
```
使用软盘绝对扇区读写工具将这个文件写入空白软盘
Linux：
```
dd if=boot.bin of=/dex/fd0 bs=512 count=1
```
Windows
```
rawrite2.exe -f boot.bin -d A
```
到这第一个操作系统就完成了，软盘此时是引导盘了
![[picture/初见引导扇区.png]]

### 引导扇区

刚刚所完成的并不是一个完整的操作系统，仅仅是一个==引导扇区==，但它是直接具有了操作系统的一个特征——在裸机上运行
计算机电源打开后会加电自检，寻找启动盘，如果选择从软盘启动，计算机会检查0面0磁道1扇区，发现它以0xAA55结束，则BIOS认为它是引导扇区(除了以0xAA55结束外，它还包含一段少于512字节的执行码)

### 代码解释

主要部分是调用了一个显示字符串的子程序，*MASM和NASM的格式总体差不多*
NASM值得说明的点：
**方括号的使用**
- 任何不被[ ]括起来的标签或变量名都被认为是地址
- 变量和标签是一样的 foo dw 1 等价于 foo: dw 1 
- Offset也是不需要的，不加方括号就是Offset
**关于\$和\$\$**
- \$表示当前行被汇编后的地址
- \$\$表示一个节开始被汇编后的地址(程序被编译后的开始地址)
- \$\$-\$表示本行距离开始处的相对距离

### 水面下的冰山

copy需要十秒，自己敲需要十分钟，调试需要一百分钟，调试才能看到水面下的冰山
Windows可以编译成.COM让他在DOS下运行调试
但我们后面会下载bochs，借用它调试不会是难事

## 搭建你的工作环境

### 虚拟计算机Bochs

虚拟机是运行在计算机内的小计算机

#### Bochs的安装

Windows和Linux安装方法略有不同
如果用的Debian GNU/Linux或近亲Ubuntu，敲下面的命令就行了
```
sudo apt-get install vgabios bochs bochs-x bximage
```
但这样安装可能省事，但是可能会没有调试功能
如果不能调试可以源代码安装，去官网下载然后
```
tar vxzf bochs-2.3.5.tar.gz
cd bochs-2.3.5
./configure --enable-debugger --enable-disasm
make
sudo make install
```
#### Bochs的使用
计算机有了，就是Bochs，还差一张空白软盘。在刚刚装好的Bochs组件中有工具叫bximage，可以生成虚拟软盘和虚拟硬盘，也称为磁盘映像、

> 使用时fd和hd选择硬盘软盘，其他的用默认影响不大，不想改敲回车就行

![[picture/bximage创建软盘.png]]

*映像：原始设备的逐字节复制，也就是说软盘的第M个字节对应映像文件的第M个字节*
计算机和软盘都准备好了，此时需要将引导扇区写进软盘，使用dd命令
```
dd if=boot.bin of=a.img bs=512 count=1 conv=notrunc
```
>conv=notrunc是防止截断的，因为boot.bin比a.image小

此时一切准备就绪，此时只需要启动了，但是还是需要配置文件的，比如告诉Bochs内存多大硬盘映像和软盘映像
配置文件
```
###############################################
# Configuration file for Bochs
###############################################
 
# 第一步，首先设置Bochs在运行过程中能够使用的内存，本例为32MB。
# 关键字为：megs
megs: 32
 
# 第二步，设置对应真实机器的BIOS和VGA BIOS.
# 对应两个关键字为：romimage 和 vgaromimage
romimage: file=$BXSHARE/BIOS-bochs-latest
vgaromimage: file=$BXSHARE/VGABIOS-lgpl-latest
 
# 第三步，设置Bochs所使用的磁盘，软盘的关键字为floppy。
# 若只有一个软盘，则使用floppya即可，若有多个，则为floppya，floppyb...
floppya: 1_44=a.img, status=inserted
 
# 第四步，选择启动盘符。
boot: floppy
 
# 第五步，设置日志文件的输出。
log: bochsout.txt
 
# 第六步，开启或关闭某些功能。
# 下面是关闭鼠标，并打开键盘。
mouse: enabled=0
keyboard_mapping: enabled=1, map=$BXSHARE/keymaps/x11-pc-us.map 
```
输入指令启动(-f后跟的配置文件)
```
bochs -f bochsrc
```
假如不带任何参数启动，Bochs将在当前目录依次寻找 .bochsrc bochsrc bochsrc.txt bochsrc.bxrc进行配置(最后一个仅对Windows有效)
也就是说直接bochs启动也是一样的
![[picture/Hello, OS World!.png]]
输入bochs然后敲一个回车之后也许不会出现我们想要的(这是有调试功能的bochs)，输入c再次回车就会出来

#### 用Bochs调试操作系统

用b 0x7c00在开始处打断点
![[picture/bochs断点调试.png]]
bochs2.3.5之前使用dump_cpu可以查看cpu情况，此时可以用r fp mmx sse dreg sreg creg等按需查看
还有很多指令，如下图：
![[picture/bochs部分指令.png]]
总之就是大胆干，bochs坏了也没事

### QEMU

如果在Linux下开发，bochs完全够用了
类似Virtual Box、Virtual PC、VM Ware各有优缺点，QEMU的优势是它可以模拟较多硬件平台

### 平台之争：Windows还是Linux

第一版是用的Windows，但本书现在用的还是Linux
使用上面平台，是口味问题或者是环境问题
Linux上的程序大都具有可拓展性以及不同程序可以协同作战，这是优点
Windows是用户友好型，但是学习收益不如Linux，程序员需要更了解自己的电脑
Linux中的工具很难过时，学习后一劳永逸

### GNU/Linux下开发环境

还有重要的东西，编辑器、编译器和自动化工具CNU Make
>编辑器选择的是vsc
>编译器选择的是GCC来编译C代码，NASM编译汇编代码
>CNU Make之后会有介绍

```
sudo apt-get install build-essential nasm
```
上面命令中build-essential包含GCC和CNU Make

==题外话：如何解决出现的问题(各种方面)==
- 向身边的朋友求助
- 使用搜索引擎看看是不是有人遇到类似的问题，那里或许已经给出解决方案
- 仔细阅读相应资料（不要怕英文），比如安装说明，或是FAQ
- 订阅相应的邮件列表（Mailing List），只要能将问题描述清楚(不会提问可以看《How To Ask Questions The Smart Way》)，通常你能在几小时内得到答复
- 到论坛提问
- 如果实在是疑难杂症，你可以试着联系软件的开发者，通常也是通过邮件列表的方式（同一个项目可 能有多个邮件列表，开发者邮件列表通常与其他分离）
- 自己阅读源代码并独立解决──这或许是个挑战，然而一旦解决了问题，你将获得知识、经验以及成功 的喜悦
**编写操作系统的步骤**
1.编译器编写代码
2.Make调用GCC、NASM及其他Linux下的工具来生成内核并写入磁盘映像
3.Bochs运行操作系统
4.调试或返回第一步

### Windows下的开发环境

我选择的还是Linux开发环境，所以此处不赘述，以后可以返回书本中了解

### 总结

开发环境搭好了，但是还不能马上开始开发，我们还需要了解硬件知识

## 保护模式(Protect Mode)

### 认识保护模式

```asm
```
上面程序定义的section没有什么实质性作用，定义它们只是使代码结构上比较清晰
我们从上面代码了解到的：
- 程序定义了一个叫做GDT的数据结构
- 后面16位代码进行了一些与GDT有关的操作
- 程序最后跳到32位代码中做了一点操作显存的工作
之后会对这些点一一解答，让我们能够自由的编写操作系统

#### 保护模式的运行环境

上面我们是吧bin文件写到引导扇区运行的，这样方便但是有隐患(==空间有限，只有512字节==)
两个方法可解决：
一：写一个引导扇区，可以读取程序并运行(但是对我们现在来说难度较大)
二：借助别的东西，比如用DOS把程序编译成COM文件，让DOS来执行

==接下来讲解一下步骤==
```pmtest1.asm
; ==========================================
; pmtest1.asm
; 编译方法：nasm pmtest1.asm -o pmtest1.bin
; ==========================================

%include "pm.inc" ; 常量, 宏, 以及一些说明

org 0100h
jmp LABEL_BEGIN

[SECTION .gdt]
; GDT
; 段基址, 段界限 , 属性
LABEL_GDT: Descriptor 0, 0, 0 ; 空描述符
LABEL_DESC_CODE32: Descriptor 0, SegCode32Len - 1, DA_C + DA_32; 非一致代码段
LABEL_DESC_VIDEO: Descriptor 0B8000h, 0ffffh, DA_DRW ; 显存首地址
; GDT 结束

GdtLen equ $ - LABEL_GDT ; GDT长度
GdtPtr dw GdtLen - 1 ; GDT界限
dd 0 ; GDT基地址

; GDT 选择子
SelectorCode32 equ LABEL_DESC_CODE32 - LABEL_GDT
SelectorVideo equ LABEL_DESC_VIDEO - LABEL_GDT
; END of [SECTION .gdt]

[SECTION .s16]
[BITS 16]
LABEL_BEGIN:
mov ax, cs
mov ds, ax
mov es, ax
mov ss, ax
mov sp, 0100h
; 初始化 32 位代码段描述符
xor eax, eax
mov ax, cs
shl eax, 4
add eax, LABEL_SEG_CODE32
mov word [LABEL_DESC_CODE32 + 2], ax
shr eax, 16
mov byte [LABEL_DESC_CODE32 + 4], al
mov byte [LABEL_DESC_CODE32 + 7], ah
; 为加载GDTR 作准备
xor eax, eax
mov ax, ds
shl eax, 4
add eax, LABEL_GDT ; eax <- gdt 基地址
mov dword [GdtPtr + 2], eax ; [GdtPtr + 2] <- gdt 基地址

; 加载 GDTR
lgdt [GdtPtr]

; 关中断
cli

; 打开地址线A20
in al, 92h
or al, 00000010b
out 92h, al

; 准备切换到保护模式
mov eax, cr0
or eax, 1
mov cr0, eax

; 真正进入保护模式
jmp dword SelectorCode32:0 ; 执行这一句会把SelectorCode32 装入cs,
; 并跳转到Code32Selector:0 处
; END of [SECTION .s16]


[SECTION .s32]; 32 位代码段. 由实模式跳入.
[BITS 32]

LABEL_SEG_CODE32:
mov ax, SelectorVideo
mov gs, ax ; 视频段选择子(目的)

mov edi, (80 * 11 + 79) * 2 ; 屏幕第11行, 第79列。
mov ah, 0Ch ; 0000:黑底 1100:红字
mov al, 'P'
mov [gs:edi], ax;

; 到此停止
jmp $

SegCode32Len equ $ - LABEL_SEG_CODE32
; END of [SECTION .s32]

```

```pm.inc
%macro Descriptor 3
dw %2 & 0FFFFh
dw %1 & 0FFFFh
db (%1 >> 16) & 0FFh
dw ((%2 >> 8) & 0F00h) | (%3 & 0F0FFh)
db (%1 >> 24) & 0FFh
%endmacro 

; 描述符类型
DA_32		EQU	4000h	; 32 位段

DA_DPL0		EQU	  00h	; DPL = 0
DA_DPL1		EQU	  20h	; DPL = 1
DA_DPL2		EQU	  40h	; DPL = 2
DA_DPL3		EQU	  60h	; DPL = 3

; 存储段描述符类型
DA_DR		EQU	90h	; 存在的只读数据段类型值
DA_DRW		EQU	92h	; 存在的可读写数据段属性值
DA_DRWA		EQU	93h	; 存在的已访问可读写数据段类型值
DA_C		EQU	98h	; 存在的只执行代码段属性值
DA_CR		EQU	9Ah	; 存在的可执行可读代码段属性值
DA_CCO		EQU	9Ch	; 存在的只执行一致代码段属性值
DA_CCOR		EQU	9Eh	; 存在的可执行可读一致代码段属性值

; 系统段描述符类型
DA_LDT		EQU	  82h	; 局部描述符表段类型值
DA_TaskGate	EQU	  85h	; 任务门类型值
DA_386TSS	EQU	  89h	; 可用 386 任务状态段类型值
DA_386CGate	EQU	  8Ch	; 386 调用门类型值
DA_386IGate	EQU	  8Eh	; 386 中断门类型值
DA_386TGate	EQU	  8Fh	; 386 陷阱门类型值

```
==上面两个文件和bochsrc放同一路径下==
- 到Bochs官方网站下载一个FreeDos。解压后将其中的a.img复制到我们的工作目录中，并改名为 freedos.img
- 用bximage生成一个软盘映像，起名为pm.img
- 修改bochsrc包含下面三句
```
floppya: 1_44=freedos.img, status=inserted 
floppyb: 1_44=pm.img, status=inserted 
boot: a
```
![[picture/保护模式运行环境1.png]]
- 启动Bochs，待FreeDos启动完毕后格式化B:盘 format b:
==注意命令行还是得按c才能弹出bochs，因为是可以调试的版本==
![[picture/保护模式运行环境2.png]]
- 将代码3.1的第8行中的07c00h改为0100h后编译
```
nasm pmtest1.asm -o pmtest1.com
```
![[picture/保护模式运行环境3.png]]
- 将pmtest1.com复制到虚拟软盘pm.img上
```
sudo mkdir  /mnt/floppy

sudo mount -o loop pm.img /mnt/floppy

sudo cp pmtest1.com  /mnt/floppy

sudo umount /mnt/floppy
```
![[picture/保护模式运行环境4.png]]
*因为已经mkdir过了，所以报错，第一次还是需要mkdir的*
- 到FreeDos中执行如下命令：
```
b:
B:\pmtest1.com
```
![[picture/保护模式运行环境5.png]]
- 运行起来出现红色字母P
#### GDT(Global Descriptor Table)
CPU有两种模式：实模式和保护模式，打开PC时是实模式，经过某种机制进入保护模式，保护模式有巨大的寻址能力
回忆一下：在学汇编的时候，8086有16位寄存器，16位数据总线，20位地址总线和1MB的寻址能力
物理地址 = 段值\*16 + 偏移
从80386开始，CPU变成32位，32位地址线，寻址空间4GB，明显16位寄存器不够了
>保护模式下仍然用段:偏移来表示地址，但“段”的概念发生根本变化。段值仍然由原来16位的cs、ds等寄存器表示，但此时仅仅变成一个索引指向一个数据结构的一个表项(定义了段的起始地址，界限，属性)——这个数据结构就是==GDT(还有一个专门的名字，描述符)==

![[picture/描述符介绍.png]]
除了上图外描述符的种类还有系统段描述符和门描述符
具体属性暂时不管，段基址和界限还是比较好理解的

==选择一个代码中的代表进行讲解==
我们现在知道GDT每一个描述符定义一个段，段寄存器和段对应的代码看下面，这是写在[SECTION.s32]中的
```
mov ax, SelectorVideo
mov gs, ax
```

```
SelectorVideo equ LABEL_DESC_VIDEO-LABEL_GDT
```
SelectorVideo是DESC_VIDEO这个描述符相对于GDT基址的偏移——专门的名称叫==选择子==
![[picture/选择子结构.png]]
![[picture/段式寻址示意图.png]]
注意经过段机制==转化成“线性地址”，而不是“物理地址”==，只是在上面的程序线性地址就是物理地址

#### 实模式到保护模式，不一般的jmp
代码中只有[SECTION .s16]这段没有讲解了，但是既然是32位的CPU，16位中的代码肯定是向保护模式跳转了
代码首先将LABEL_SEG_CODE32的物理地址赋给eax，然后分成三部分赋值给描述符DESC_CODE32中的相应位置
接下来把GDT的物理地址填充到了GdtPtr这个6字节的数据结构，然后执行lgdt [GdtPtr] 加载到寄存器gdtr中
![[picture/gdtr示意图.png]]
接下来是关中断，因为保护模式下中断机制是不同的，不关中断将会出现错误
再下面几句是打开A20地址线，因为历史问题，8086只能寻址1MB，超过则**回卷**，但是80286时，访问范围变大了，不会再回卷，为了保证兼容性，使用8042键盘控制器来控制20个，这就是A20，不被打开则20位永远是0
我们现在需要打开，比较麻烦，上面是一个方法，还有其他方法

只差一段代码就到那个特别的jmp了，这一段代码只是把寄存器cr0的第0位置为1(==这正是决定实模式和保护模式的关键，0是实模式，1是保护模式==)
![[picture/cr0.png]]
为什么我们还需要jmp指令呢
```
jmp dword SelectorCode32:0
```
因为需要改cs的值
总结：进入保护模式的主要步骤
- 准备GDT
- 用lgdt加载gdtr
- 打开A20
- 置cr0的PE位
- 跳转，进入保护模式

#### 描述符属性(参考，了解即可)
对上面按下不表的一个补充
- P位存在（Present）位。P=1表示段在内存中存在；P=0表示段在内存中不存在
- DPL描述符特权级（Descriptor Privilege Level）。特权级可以是0、1、2或者3。数字越小特权级越大
- S位指明描述符是数据段／代码段描述符（S=1）还是系统段／门描述符（S=0）
- TYPE描述符类型
![[picture/描述符类型.png]]
- G位段界限粒度（Granularity）位。G=0时段界限粒度为字节；G=1时段界限粒度为4KB
- D/B位这一位比较复杂，分三种情况
   - 在可执行代码段描述符中，这一位叫做D位。D=1时，在默认情况下指令使用32位地址及32位或8 位操作数；D=0时，在默认情况下使用16位地址及16位或8位操作数
   - 在向下扩展数据段的描述符中，这一位叫做B位。B=1时，段的上部界限为4GB；B=0时，段的上 部界限为64KB
   - 在描述堆栈段（由ss寄存器指向的段）的描述符中，这一位叫做B位。B=1时，隐式的堆栈访问指令（如push、pop和call）使用32位堆栈指针寄存器esp；D=0时，隐式的堆栈访问指令（如push、 pop和call）使用16位堆栈指针寄存器sp
- AVL位保留位，可以被系统软件使用

>是“一致码段”中“一致”，当转移的目标是一个特权级更高的一致代码段，当前的特权级会被延续下去，而向特权级更高的非一致代码段的转移会引起常规保护错误，目标代码的特权级低的话，无论它是不是一致代码段，都不能通过call或者jmp转移 进去，尝试这样的转移将会导致常规保护错误

总之看图
![[picture/一致与非一致.png]]

### 保护模式进阶

虽然进入了保护模式，但是并没有体验到保护模式的好处，接下来继续看

#### 海阔凭雨跃

上面的程序为了让代码最短，进入保护模式之后开始死循环，除了重启系统外没有其他办法，这是作者为了便于我们理解，实际上完全可以程序结束时跳回实模式

从实模式进入保护模式用一个跳转就可以了，但是返回时却要稍微复杂一点，因为我们需要加载何时的描述符选择子到有关寄存器，**我们还只能从16位代码段返回**

返回实模式用到的代码：
```asm
[SECTION.s16code]
ALIGN 32
[BITS 16]
LABEL_SEG_CODE16:
; 跳回实模式:
mov ax, SelectorNormal;在之前的新增一个Normal描述符
mov ds, ax
mov es, ax
mov fs, ax
mov gs, ax
mov ss, ax
mov eax, cr0
and al, 11111110b
mov cr0, eax
LABEL_GO_BACK_TO_REAL:
jmp 0:LABEL_REAL_ENTRY ; 段地址会在程序开始处被设置成正确的值
Code16Len equ $-LABEL_SEG_CODE16

; END of [SECTION.s16code]
```

#### LDT

类似于GDT，都是描述符表
与LDT相比，选择子的TI位必须置为1，运用时先加载ldtr
保护模式中的保护的意思：
- 段地址和段界限定义了一个段的范围，对超越界限之外的地址访问是被禁止的
- 有点复杂的段属性作为一个对段各个方面的定义规定和限制了段的行为和性质

#### 特权级概述

特权级总共4个——0、1、2、3，数字越小表示特权级越大
处理器将用这样的机制来避免低特权级在不允许的情况下访问高特权级的段
![[picture/特权级.png]]

为了避免混淆，也将特权级称做内层，低特权级称外层

#### CPL、DPL、RPL

**1. CPL(Current Privilege Level)**
CPL是当前执行的程序或任务的特权级，CPL等于代码所在段的特权级。当程序转移到不同特权级时，处理器将改变CPL
*特殊情况：遇到一致代码段时，一致代码段可以被相同或更低特权级的代码访问，当访问一个CPL特权级不同的一致代码时，CPL不会改变*

**2. DPL(Descriptor Privilege Level)**

DPL表示段和门的特权级




------------------------------------------------------------------------------------

😀😀😀😀😀这里实在是难以再往后学习(汇编实在是基础不牢)，操作系统的实现目前来说也不是这么的重要，所以还是暂时停止后续学习吧，以后要是有机会再自己实现自己的操作系统吧